AC_PREREQ(2.57)

AC_INIT([sigscheme], [0.5.0], [mover@hct.zaq.ne.jp])
AM_INIT_AUTOMAKE(1.8.3)

AC_CONFIG_SRCDIR([src/sigscheme.h])
AC_CONFIG_HEADER([config.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_LIBTOOL
AC_PATH_PROG(has_ruby, ruby, no)
if test x$has_ruby = xno ; then
  AC_MSG_ERROR([please install ruby (http://www.ruby-lang.org/)])
fi

# Checks for libraries.

AX_LIB_GLIBC

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([limits.h malloc.h stddef.h stdlib.h string.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_VOLATILE
#AC_C_LONG_LONG
#AC_C_LONG_DOUBLE
AC_C_CHAR_UNSIGNED
AC_C_BIGENDIAN
AC_C_STRINGIZE
AX_C___ATTRIBUTE__

AX_CREATE_STDINT_H(src/my-stdint.h)
AC_TYPE_SIZE_T

AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
#AC_CHECK_SIZEOF(float)
#AC_CHECK_SIZEOF(double)
#AC_CHECK_SIZEOF(long double)
AC_CHECK_SIZEOF(void *)
AC_CHECK_SIZEOF(size_t)

# Do not assume (sizeof(int32_t) == 4) and so on (i.e. do not (CHAR_BIT == 8)).
AC_CHECK_SIZEOF(int16_t)
AC_CHECK_SIZEOF(int32_t)
AC_CHECK_SIZEOF(int64_t)
#AC_CHECK_SIZEOF(int_least8_t)
#AC_CHECK_SIZEOF(int_least16_t)
#AC_CHECK_SIZEOF(int_least32_t)
#AC_CHECK_SIZEOF(int_least64_t)
#AC_CHECK_SIZEOF(int_fast8_t)
#AC_CHECK_SIZEOF(int_fast16_t)
AC_CHECK_SIZEOF(int_fast32_t)
#AC_CHECK_SIZEOF(int_fast64_t)
AC_CHECK_SIZEOF(intmax_t)
AC_CHECK_SIZEOF(intptr_t)

# Checks for library functions.

# FIXME: provide alternative source code
# AC_REPLACE_FUNCS([snprintf asprintf vasprintf])
AC_CHECK_FUNCS([strdup strcasecmp \
                snprintf asprintf vasprintf \
                fileno posix_memalign getpagesize])

AX_CHECK_PAGE_ALIGNED_MALLOC

# Configure Option
AC_ARG_ENABLE(debug,
  AC_HELP_STRING([--enable-debug],
                 [enable debugging]),
  [],
  [
    enable_debug="no"
  ])
if test x$enable_debug = xyes; then
	CFLAGS="$CFLAGS -g -DDEBUG"
else
	CFLAGS="$CFLAGS -O2"
fi

# Default Compiler Option for gcc
if test x$CC = xgcc; then
	CFLAGS="$CFLAGS -Wall -std=gnu89 -pedantic -Wchar-subscripts -Wmissing-declarations -Wmissing-prototypes -Wnested-externs -Wpointer-arith -Wcast-align -Wsign-compare"
fi

AC_CONFIG_FILES([Makefile
                 m4/Makefile
                 src/Makefile])
AC_OUTPUT
